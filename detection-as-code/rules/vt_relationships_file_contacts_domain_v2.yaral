rule vt_relationships_file_contacts_domain_v2 {
meta:
  author = "Google Cloud Threat Intelligence"
  description = "Detects a Network Connection to a known Domain artifact and Process Hash in Virus Total, augmented with low prevalence scoring from Chronicle Derived Context."
  severity = "MEDIUM"
  priority = "LOW"
events:
  // event
   $e.metadata.event_type = "NETWORK_CONNECTION" or
   $e.metadata.event_type = "NETWORK_DNS"
   $e.principal.process.file.sha256 = $file_hash
   strings.coalesce($e.target.hostname, $e.network.dns.questions.name) = $domain
   strings.coalesce($e.principal.hostname, $e.principal.ip) = $sourceHost
   $e.principal.namespace = $namespace
   $vtUrl = strings.concat("https://www.virustotal.com/gui/domain/", strings.coalesce($e.target.hostname, $e.network.dns.questions.name))
   // VT Entity Graph
  $vt.graph.metadata.entity_type = "FILE"
  $vt.graph.metadata.source_type = "GLOBAL_CONTEXT"
  $vt.graph.metadata.vendor_name = "VirusTotal"
  $vt.graph.metadata.product_name = "VirusTotal Relationships"
  $vt.graph.entity.file.sha256 = $file_hash
   // VT Entity Graph Relationship
  $vt.graph.relations.entity_type = "DOMAIN_NAME"
  $vt.graph.relations.relationship = "CONTACTS"
  $vt.graph.relations.entity.domain.name = $domain
   // // Chronicle Derived Prevalence
   $prevalence.graph.metadata.source_type = "DERIVED_CONTEXT"
   $prevalence.graph.metadata.entity_type = "DOMAIN_NAME"
   $prevalence.graph.entity.domain.name = $domain
   $prevalence.graph.entity.domain.prevalence.day_max > 0
match:
  $sourceHost over 5m
outcome:
   $risk_score = max(
       if($prevalence.graph.entity.domain.prevalence.day_max = 1, 90,0) +
       if($prevalence.graph.entity.domain.prevalence.day_max > 1 and $prevalence.graph.entity.domain.prevalence.day_max < 5, 80,0) +
       if($prevalence.graph.entity.domain.prevalence.day_max > 4 and $prevalence.graph.entity.domain.prevalence.day_max < 10, 50,0) +              
       if($prevalence.graph.entity.domain.prevalence.day_max > 9, 10,0)       
   )
   // Aggregates
   $count_principalSha256 = count_distinct($file_hash)
   $count_targetDomain = count_distinct($domain)
   // Pivot URL
   $url_principalSha256 = array_distinct($vtUrl)
   // Alert Graph
   $principalNamespace = array_distinct($namespace)
   $targetDomain = array_distinct($domain)
   $principalSha256 = array_distinct($file_hash)
   $principalHostname = array_distinct($sourceHost)
condition:
  $e and $vt and $prevalence and $risk_score > 50
}
